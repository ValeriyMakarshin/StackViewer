apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = 6
def versionSuffix = versionBuild > 0 ? "-beta" : ""

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.hodzi.stackviewer"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}${versionSuffix}"
        testInstrumentationRunner 'com.hodzi.stackviewer.CustomAndroidTestRunner'

        flavorDimensions "default"
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError true
        warningsAsErrors true
        lintConfig file("$projectDir/lint.xml")
        textReport true
        textOutput "stdout"
        disable 'RestrictedApi'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
        exclude 'META-INF/rxjava.properties'
    }

    productFlavors {
        staging {
            applicationIdSuffix '.staging'
            versionName android.defaultConfig.versionName + "-STAGING"
        }

        mock {
            applicationIdSuffix '.mock'
            versionName android.defaultConfig.versionName + "-MOCK"
        }

        prod {
        }
    }

}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

androidExtensions {
    experimental = true
}

kapt {
//    generateStubs = true
}

apply from: 'deps.gradle'

dependencies {
    implementation supportLibs
    implementation kotlinLibs
    implementation rxJavaLibs
    implementation retrofitLibs
    implementation okHttpLibs
    implementation stethoLibs
    implementation daggerLibs
    implementation 'com.github.bumptech.glide:glide:4.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    kapt kaptLibs
    kapt 'com.github.bumptech.glide:compiler:4.2.0'
    kapt 'com.google.dagger:dagger-compiler:2.11'

    testImplementation testsLibs
    testImplementation kotlinTestLibs
    testImplementation 'junit:junit:4.12'

    androidTestImplementation androidTestsLibs
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'com.github.andrzejchm.RESTMock:android:0.3.1'
}
repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com'
    }
    maven {
        url 'https://jitpack.io'
    }
}

